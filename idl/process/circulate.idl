#include "base_data_types.idl"

module nec
{
    module process
    {
        //@copy-c-declaration static const char CIRCULATE_REQUEST[] = "CirculateRequest";

        /**
         * \brief requested state change in the circulation system.
         *
         * \ingroup SafeCirculateFunctions
         *
         */
        struct CirculateRequest
        {
            DataTypes::Uuid     id; //@key   ///< id of requestor
            DataTypes::Uuid     objectiveId; ///< id of current objective
            DataTypes::Priority priority;    ///< priority of the request
            DataTypes::Time     timeNeeded;  ///< time requestor needs the resource
            DataTypes::Time     estimatedDuration; ///< estimated duration requestor needs the resource
            double              targetFlowRate; ///< requested flow rate in m/sec^3
        };

        //@copy-c-declaration static const char CIRCULATE_OBJECTIVE[] = "CirculateObjective";

        /**
         * \brief current requested state change in the circulation system.
         *
         * \ingroup SafeCirculateFunctions
         */
        struct CirculateObjective
        {
            DataTypes::Uuid     id; //@key   ///< id of requestor
            DataTypes::Uuid     objectiveId; ///< id of current objective
            DataTypes::Time     estimatedDuration; ///< estimated duration requestor needs the resource
            double              targetFlowRate; ///< requested flow rate in m/sec^3
        };

        //@copy-c-declaration static const char CIRCULATE_STATE[] = "CirculateState";

        /**
         * \brief current state of circulation system.
         *
         * \ingroup SafeCirculateFunctions
         *
         * | Variable                | Description                            | Data Type         | Unit    |
         * | ----------------------- | ---------------------------------------| ------------------| ------- |
         * | id                      | id of requestor                        | DataTypes::Uuid   | N/A     |
         * | timestamp               | timestamp when the data was published. | DataTypes::Time   | sec     |
         * | status                  | status of ciruclation system           | Datatypes::Status | N/A     |
         * | actualFlowRate          | actual flow rate                       | double            | m/sec^3 |
         * | actualStandpipePressure | actual standpipe pressure              | double            | Pa      |
         * | minFlowRate             | minimum machine flow rate              | double            | m/sec^3 |
         * | maxFlowRate             | maximum machine flow rate              | double            | m/sec^3 |
         * | minStandpipePressure    | minimum machine standpipe pressure     | double            | Pa      |
         * | maxStandpipePressure    | maximum machine standpipe pressure     | double            | Pa      |
         * | targetFlowRate          | requetsed flow rate                    | double            | m/sec^3 |
         */
        struct CirculateState
        {
            DataTypes::Uuid   id;           //@key
            DataTypes::Uuid   objectiveId; 
            DataTypes::Time   timestamp;
            DataTypes::Status status;

            // actuals
            double            actualFlowRate;
            double            actualStandpipePressure;

            // constraints
            double            minFlowRate;
            double            maxFlowRate;
            double            minStandpipePressure;
            double            maxStandpipePressure;

            // targets
            double            targetFlowRate;
        };
    };
};
